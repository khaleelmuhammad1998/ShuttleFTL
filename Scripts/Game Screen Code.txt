// Libraries
import flash.events.Event;
import flash.events.TouchEvent;
import flash.events.KeyboardEvent;
import flash.display.MovieClip;
import flash.ui.Multitouch;
import flash.ui.MultitouchInputMode;
import flash.events.MouseEvent;

stop();

// Function for generating a random number.
function randomizeNumber(NumberMin: Number, NumberMax: Number): Number {
	return (Math.floor(Math.random() * (NumberMax - NumberMin + 1)) + NumberMin);
}

// Function to clamp number between two values.
function clampNumber(Value:Number, NumberMin:Number, NumberMax:Number): Number {
    return Math.max(NumberMin, Math.min(NumberMax, Value));
}

// Declaration of variables.
var GameInitialised: Boolean = false;
var GamePaused: Boolean = true;
var GamePauseLayer: Array = [new PauseLayer(), new ButtonResume()];
var Score: Number = 0;
var PlayerSpeed: Number = 15;
var PlayerHealth: Number = 3;
var Players: Array = [new Player(), new Player2(), new Player3(), new Player4(), new Player5()];
var PlayerNumber: Number = randomizeNumber(0, 4);
var PlayerCurrentPosition: Number = Players[PlayerNumber].x;
var GameLevel: Number = 1;
var GameSpeedMin: Number = 15;
var GameSpeedMax: Number = 25;
var GameHealthMax: Number = 3;
var Opponents: Array = [new Opponent1(), new Opponent2(), new Opponent3(), new PowerUp(), new PowerUp2(), new PowerUp3()];
var Opponents2: Array = [new Opponent1(), new Opponent2(), new Opponent3()];
var OpponentNumber: Number = randomizeNumber(0, 2);
var Opponent2Number: Number = randomizeNumber(0, 2);
var	Opponent2Enabled: Number = 0;
var SoundScoreVariable: Sound = new (SoundScore);
var SoundPowerUpVariable: Sound = new (SoundPowerUp);
var SoundEndVariable: Sound = new (SoundEnd);
var ExplosionVariable: MovieClip = new Explosion();

// Code to start the game.
startGame();

// Function to update game level.
function updateGameLevel() {
	if (Score <= 100) {
		GameSpeedMin = 15;
		GameSpeedMax = 25;
		GameHealthMax = 3;
		GameLevel = 1;
	} else if (Score <= 250) {
		GameSpeedMin = 20;
		GameSpeedMax = 30;
		GameHealthMax = 5;
		GameLevel = 2;
	} else {
		GameSpeedMin = 25;
		GameSpeedMax = 30;
		GameHealthMax = 5;
		GameLevel = 3;
	}
}

// Function for updating game's speed.
function updatePlayerSpeed() {
	if (stage.frameRate <= 30) {
		stage.frameRate = stage.frameRate + 1;
	} else {
		stage.frameRate = 30;
		if (PlayerSpeed < GameSpeedMax) {
			PlayerSpeed += 1;
		} else {
			PlayerSpeed = GameSpeedMax;
		}
	}
}

// Function for resetting opponents.
function resetOpponents() {
	// Code to reset the opponents.
	Opponents[OpponentNumber].y = -25;
	Opponents[OpponentNumber].x = 75;
	Opponents2[Opponent2Number].y = -25;
	Opponents2[Opponent2Number].x = 75;

	// Code to shuffle opponents.
	OpponentNumber = randomizeNumber(0, 5);
	Opponent2Number = randomizeNumber(0, 2);

	// Code to activate opponents.
	Opponents[OpponentNumber].x = (randomizeNumber(1, 3)) * 75;
	Opponent2Enabled = randomizeNumber(0, 1);
	Opponents2[Opponent2Number].x = (randomizeNumber(1, 3)) * 75;
	if (Opponent2Enabled == 1 && Opponents2[Opponent2Number].x == Opponents[OpponentNumber].x) {
		Opponent2Enabled = 0;
		Opponents2[Opponent2Number].x = 75;
	}
}

// Function to generate UI when game is paused.
function pauseGameUI() {
	if (GamePaused == true) {
		stage.addChild(GamePauseLayer[0]);
		stage.addChild(GamePauseLayer[1]);
		GamePauseLayer[1].x = 150;
		GamePauseLayer[1].y = 266;
		GamePauseLayer[1].addEventListener(MouseEvent.CLICK, pauseButton);
	} else {		
		GamePauseLayer[1].removeEventListener(MouseEvent.CLICK, pauseButton);
		stage.removeChild(GamePauseLayer[1]);
		stage.removeChild(GamePauseLayer[0]);
	}
}

// Function for game's UI.
// Gameplay Event Number 0 = Negative, 1 = Neutral, 2 = Score, 3 = PlayerSpeed, 4 = PlayerHealth
function gameUI(GameplayEventNumber: Number, PositiveUpdate: Boolean) {
	var ColourWhite = 0xFFFFFF;
	var ColourRed = 0xFF4488;
	var GameLevelUI = "EASY";

	TextScore.text = String(Score);
	TextScore.textColor = ColourWhite;
	TextSpeed.text = String(PlayerSpeed);
	TextSpeed.textColor = ColourWhite;
	TextHealth.text = String(PlayerHealth);
	TextHealth.textColor = ColourWhite;
	if (GameplayEventNumber == 0) {
		TextScore.textColor = ColourRed;
		TextSpeed.textColor = ColourRed;
		TextHealth.textColor = ColourRed;
	} else if (GameplayEventNumber == 2) {
		TextScore.textColor = 0x00CCFF;
	} else if (GameplayEventNumber == 3) {
		if (PositiveUpdate == true) {
			TextSpeed.textColor = 0x00FFFF;
		} else {
			TextSpeed.textColor = ColourRed;
		}
	} else if (GameplayEventNumber == 4) {
		if (PositiveUpdate == true) {
			TextHealth.textColor = 0x00FFCC;
		} else {
			TextHealth.textColor = ColourRed;
		}
	}

	switch (GameLevel) {
		case 1:
			GameLevelUI= "EASY";
			break;
		case 2:
			GameLevelUI = "MID";
			break;
		case 3:
			GameLevelUI = "HARD";
			break;
	}
	TextLevel.text = GameLevelUI;
}

// Function for playing game's sound effects.
// Gameplay Event Number 0 = Negative, 1 = Neutral, 2 = Positive
function gameSoundEffect(GameplayEventNumber: Number) {
	if (SoundEnabled == true) {
		if (GameplayEventNumber == 0) {
			SoundEndVariable.play();
		} else if (GameplayEventNumber == 1) {
			SoundScoreVariable.play();
		} else if (GameplayEventNumber == 2) {
			SoundPowerUpVariable.play();
		}
	}
}

// Function to start the game.
function startGame() {
	stage.focus = this;
	stage.frameRate = 30;
	Multitouch.inputMode = MultitouchInputMode.TOUCH_POINT;
	stage.addEventListener(TouchEvent.TOUCH_BEGIN, touchInputManager);
	stage.addEventListener(KeyboardEvent.KEY_DOWN, keyInputManager);
	ButtonPause.addEventListener(MouseEvent.CLICK, pauseButton);
	stage.addEventListener(Event.ENTER_FRAME, gameManager);

	if (GameInitialised == false) {
		// Code to setup the player.
		Players[PlayerNumber].x = 150;
		PlayerCurrentPosition = Players[PlayerNumber].x;
		stage.addChild(Players[PlayerNumber]);
		Players[PlayerNumber].y = 470;

		// Code to spawn opponents and power ups.
		for (var index: int = 0; index < Opponents.length; index++) {
		   	Opponents[index].x = 75;
		   	Opponents[index].y = -25;
		   	stage.addChild(Opponents[index]);
		}
		for (index = 0; index < Opponents2.length; index++) {
		   	Opponents2[index].x = 75;
		   	Opponents2[index].y = -25;
		   	stage.addChild(Opponents2[index]);
		}
		stage.addChild(AntiCheatCover);
		GameInitialised = true;
	}
	gameUI(1, false);
	GamePaused = false;	
}

// Function to pause the game.
function pauseGame() {
	if (GamePaused == false) {
		stage.removeEventListener(Event.ENTER_FRAME, gameManager);
		GamePaused = true;
	} else {
		stage.addEventListener(Event.ENTER_FRAME, gameManager);
		GamePaused = false;
	}
	pauseGameUI();
}

// Function to respond to pause button.
function pauseButton(event: MouseEvent) {
	pauseGame();
}

// Function to end the game.
function endGame(event: Event) {
	GamePaused = true;
	stage.removeEventListener(KeyboardEvent.KEY_DOWN, keyInputManager);
	stage.removeEventListener(TouchEvent.TOUCH_BEGIN, touchInputManager);
	ButtonPause.removeEventListener(MouseEvent.CLICK, pauseButton);
	stage.removeEventListener(Event.ENTER_FRAME, gameManager);
	stage.frameRate = 30;

	// Code to remove opponents and power ups.
	for (var index: int = 0; index < Opponents.length; index++) {
		if (Opponents[index].y == -25) {
	   		stage.removeChild(Opponents[index]);
		}
	}
	for (index = 0; index < Opponents2.length; index++) {
		if (Opponents2[index].y == -25) {
	   		stage.removeChild(Opponents2[index]);
		}
	}
	stage.removeChild(AntiCheatCover);

	// Code to add explosion effect to player.
	stage.removeChild(Players[PlayerNumber]);
	stage.addChild(ExplosionVariable);
	ExplosionVariable.y = 470;
	ExplosionVariable.x = PlayerCurrentPosition;

	// Code to change scene.
	gotoAndStop(1, "End Screen");
}

// Function for controlling the playable character using touch screen.
function touchInputManager(event: TouchEvent) {
	if (GamePaused == false) {
    	if (event.stageX <= 150 && Players[PlayerNumber].x > 75) {
			Players[PlayerNumber].y = 350;
    	    Players[PlayerNumber].x = Players[PlayerNumber].x - 75;
			PlayerCurrentPosition = Players[PlayerNumber].x;
		} else if (event.stageX > 150 && Players[PlayerNumber].x < 225) {
    	   	Players[PlayerNumber].y = 350;
			Players[PlayerNumber].x = Players[PlayerNumber].x + 75;
			PlayerCurrentPosition = Players[PlayerNumber].x;
		}
	}
}

// Function for controlling the playable character using keyboard.
function keyInputManager(event: KeyboardEvent) {
    if (event.keyCode == Keyboard.LEFT && Players[PlayerNumber].x > 75 && GamePaused == false) {
		Players[PlayerNumber].y = 350;
        Players[PlayerNumber].x = Players[PlayerNumber].x - 75;
		PlayerCurrentPosition = Players[PlayerNumber].x;
	} else if (event.keyCode == Keyboard.RIGHT && Players[PlayerNumber].x < 225 && GamePaused == false) {
		Players[PlayerNumber].y = 350;
		Players[PlayerNumber].x = Players[PlayerNumber].x + 75;
		PlayerCurrentPosition = Players[PlayerNumber].x;
	} else if (event.keyCode == Keyboard.ESCAPE) {
		pauseGame();
	}
}

// Function for managing the game.
function gameManager(event: Event) {
	Players[PlayerNumber].y = 470;
	Opponents[OpponentNumber].y = Opponents[OpponentNumber].y + PlayerSpeed;
	if (Opponent2Enabled == 1) {
		Opponents2[Opponent2Number].y = Opponents2[Opponent2Number].y + PlayerSpeed;
	}
	if (Opponents[OpponentNumber].y > 470 || Opponents2[Opponent2Number].y > 470) {
		Score = Score + 1 + Opponent2Enabled;
		resetOpponents();
		gameUI(1, false);
		gameSoundEffect(1);
		updatePlayerSpeed();
	}

	// Conditions for altering game's behaviour.
	if ((Players[PlayerNumber].hitTestObject(Opponents[OpponentNumber]) && OpponentNumber < 3) || Players[PlayerNumber].hitTestObject(Opponents2[Opponent2Number])) {
		if (PlayerHealth > 1) {
			PlayerHealth--;
			resetOpponents();
			gameUI(4, false);
			gameSoundEffect(1);
			updatePlayerSpeed();
		} else {
			gameUI(0, false);
			gameSoundEffect(0);
			endGame(event);
		}
	} else if (Players[PlayerNumber].hitTestObject(Opponents[3])) {
		Score = Score + 10;
		resetOpponents();
		gameUI(2, true);
		gameSoundEffect(2);
		updatePlayerSpeed();
	} else if (Players[PlayerNumber].hitTestObject(Opponents[4])) {
		if (PlayerSpeed >= 20) {
			PlayerSpeed = clampNumber((PlayerSpeed - 3), GameSpeedMin, GameSpeedMax);
		} else {
			updatePlayerSpeed();
		}
			gameUI(3, true);
			gameSoundEffect(2);
		resetOpponents();
	} else if (Players[PlayerNumber].hitTestObject(Opponents[5])) {
		if (PlayerHealth < GameHealthMax) {
			PlayerHealth++;
		}
		gameUI(4, true);
		gameSoundEffect(2);
		resetOpponents();
		updatePlayerSpeed();
	}
	updateGameLevel();
}